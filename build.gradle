buildscript {
  dependencies {
    classpath("org.flywaydb:flyway-database-postgresql:11.8.2")
  }
}

plugins {
  id 'application'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.1.7'
  id 'org.springframework.boot' version '3.3.10'
  id 'com.github.ben-manes.versions' version '0.52.0'
  id 'org.sonarqube' version '6.2.0.5505'
  id 'uk.gov.hmcts.java' version '0.12.66'
  id 'org.flywaydb.flyway' version '11.8.2'
  id 'net.serenity-bdd.serenity-gradle-plugin' version '4.2.30'
}

group = 'uk.gov.hmcts'
version = '0.0.1'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

}

configurations {
  functionalTestImplementation.extendsFrom testImplementation
  functionalTestRuntimeOnly.extendsFrom runtimeOnly

  integrationTestImplementation.extendsFrom testImplementation
  integrationTestRuntimeOnly.extendsFrom runtimeOnly

  smokeTestImplementation.extendsFrom testImplementation
  smokeTestRuntimeOnly.extendsFrom runtimeOnly
}

tasks.withType(JavaCompile).configureEach {
  options.compilerArgs << "-Xlint:unchecked"
//  Enable the following to highlight all deprecated build errors
  options.compilerArgs << "-Xlint:deprecation"
//  Enable the following to stop the build on any warning messages (not just error messages)
  options.compilerArgs << "-Werror"
//  Change the maximum number of reported compiler errors (default 100)
  options.compilerArgs.addAll("-Xmaxerrs", "200")
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

tasks.withType(Test).configureEach {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

test {
  failFast = true
}

task integration(type: Test) {
  description = "Runs integration tests"
  group = "Verification"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  failFast = true
}

task functionalOpal(type: Test) {

  description = "Runs functional tests against Opal mode"
  group = "Verification"
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
  outputs.upToDateWhen { false }
  reports.junitXml.getOutputLocation().set(layout.buildDirectory.dir("test-results/functional/opal"))
  testLogging.showStandardStreams = true
  gradle.startParameter.continueOnFailure = true
  systemProperty "test.mode", "opal"

  include '**/OpalTestRunner.class'

}
task functionalLegacy(type: Test) {
  description = "Runs functional tests against Legacy mode"
  group = "Verification"
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
  outputs.upToDateWhen { false }
  reports.junitXml.getOutputLocation().set(layout.buildDirectory.dir("test-results/functional/legacy"))
  gradle.startParameter.continueOnFailure = true
  testLogging.showStandardStreams = true
  systemProperty "test.mode", "legacy"

  include '**/LegacyTestRunner.class'
}
task copyFunctionalReport(type: Copy) {
  from("${rootDir}/target/site/serenity")
  into("${rootDir}/functional-test-report")
  logger.quiet("Functional Test Report available at - file://${rootDir}/functional-test-report/index.html")
}
task mergeFunctionalResults(type: Copy) {
  from layout.buildDirectory.dir("test-results/functional/opal")
  from layout.buildDirectory.dir("test-results/functional/legacy")
  into layout.buildDirectory.dir("test-results/functional")
  include '*.xml'
}
task generateTestSummary(type: JavaExec) {
  group = "Reporting"
  description = "Generate an HTML summary from test result XMLs"
  classpath = sourceSets.functionalTest.runtimeClasspath
  mainClass = 'uk.gov.hmcts.opal.scripts.TestReportSummaryGenerator'
}

task functional() {
  description = "Runs functional tests"
  group = "Verification"
  gradle.startParameter.continueOnFailure = true
  dependsOn('clearReports', 'functionalOpal', 'functionalLegacy', 'aggregate', 'copyFunctionalReport','mergeFunctionalResults', 'generateTestSummary')
  tasks.functionalOpal.mustRunAfter clearReports
  tasks.functionalLegacy.mustRunAfter functionalOpal
  tasks.mergeFunctionalResults.mustRunAfter functionalLegacy
  tasks.aggregate.mustRunAfter functionalLegacy
  tasks.copyFunctionalReport.mustRunAfter aggregate
  tasks.generateTestSummary.mustRunAfter copyFunctionalReport

}
task smokeOpal(type: Test) {
  description = "Runs Smoke Tests"
  group = "Verification"
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
  reports.junitXml.getOutputLocation().set(layout.buildDirectory.dir("test-results/smoke"))
  testLogging.showStandardStreams = true
  gradle.startParameter.continueOnFailure = true

  include '**/SmokeTestRunner.class'

}
task copySmokeReport(type: Copy) {
  from("${rootDir}/target/site/serenity")
  into("${rootDir}/smoke-test-report")
  logger.quiet("Smoke Test Report available at - file://${rootDir}/smoke-test-report/index.html")
}

task smoke() {
  description = "Runs Smoke Tests"
  group = "Verification"
  gradle.startParameter.continueOnFailure = true
  dependsOn('clearReports', 'smokeOpal', 'aggregate', 'copySmokeReport')
  tasks.smokeOpal.mustRunAfter clearReports
  tasks.aggregate.mustRunAfter smokeOpal
  tasks.copySmokeReport.mustRunAfter aggregate

}

def coverageExclusions = [
  '**/uk/gov/hmcts/opal/**/model/**',
  '**/uk/gov/hmcts/opal/**/config/**',
  '**/uk/gov/hmcts/opal/authentication/client/**',
  '**/uk/gov/hmcts/opal/**/exception/**'
]

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.required = true
    html.required = true
    csv.required = false
    html.outputLocation.set(layout.buildDirectory.dir("jacocoHtml"))
  }
  afterEvaluate {
    classDirectories.setFrom(files(classDirectories.files.collect {
      fileTree(dir: it, exclude: jacocoExclusionArray(coverageExclusions)
      )
    }))
  }
}

static String[] jacocoExclusionArray(ArrayList<String> exclusions) {
  final def lst = new ArrayList<String>()

  exclusions.stream().forEach { it.endsWith(".java") ? lst.add(it.replace(".java", ".class")) : lst.add(it) }

  return lst.toArray()
}

project.tasks['sonarqube'].dependsOn jacocoTestReport
project.tasks['check'].dependsOn integration

jacocoTestReport.dependsOn check
jacocoTestCoverageVerification.dependsOn jacocoTestReport

sonarqube {
  properties {
    property "sonar.projectName", "HMCTS :: opal-fines-service"
    property "sonar.projectKey", "uk.gov.hmcts:opal-fines-service"
    property "sonar.gradle.skipCompile", "true"
    property "sonar.exclusions", coverageExclusions.join(', ')
    property 'sonar.coverage.exclusions', "**/entity/*,**/model/*,**/exception/*,**/config/**,**/repository/jpa/*,**/opal/dto/*"
  }
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

dependencyManagement {
  imports {
    mavenBom 'org.springframework.cloud:spring-cloud-dependencies:2023.0.5'
  }

  imports {
    mavenBom "org.springframework.integration:spring-integration-bom:6.4.4"
  }

  dependencies {
    dependency group: 'com.google.guava', name: 'guava', version: '33.4.8-jre'
  }
}

dependencyCheck {
  suppressionFile = 'config/owasp/suppressions.xml'
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url 'https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1'
  }
}

ext {
  log4JVersion = "2.24.3"
  serenityVersion = "4.2.30"
}

ext['snakeyaml.version'] = '2.0'
ext['jackson.version'] = '2.16.1'
ext['tomcat.version'] = '10.1.40'

dependencies {
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-oauth2-client'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-quartz'
  implementation 'org.springframework.boot:spring-boot-starter-data-redis'
  implementation 'org.springframework.boot:spring-boot-starter-cache'

  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-integration'
  implementation group: 'org.springframework.integration', name: 'spring-integration-sftp'
  implementation group: 'org.springframework.integration', name: 'spring-integration-file'


  implementation group: 'org.springframework.security', name: 'spring-security-oauth2-authorization-server', version: '1.4.3'

  implementation group: 'org.springframework', name: 'spring-aspects'

  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-netflix-archaius', version: '2.2.10.RELEASE'

  implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.6.0'

  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: '6.1.9'

  implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4JVersion
  implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: log4JVersion
  implementation group: 'com.google.guava', name: 'guava', version: '33.4.8-jre'

  implementation group: 'com.launchdarkly', name: 'launchdarkly-java-server-sdk', version: '7.8.0'

  implementation(group: 'com.networknt', name: 'json-schema-validator', version: '1.5.6');
  implementation(group: 'com.jayway.jsonpath', name: 'json-path', version: '2.9.0');

  implementation group: 'io.rest-assured', name: 'rest-assured'
  implementation 'org.flywaydb:flyway-core'
  runtimeOnly 'org.flywaydb:flyway-database-postgresql:11.8.2'
  implementation 'org.postgresql:postgresql'

  implementation 'com.fasterxml.jackson.core:jackson-databind:2.16.2'
  compileOnly 'org.projectlombok:lombok:1.18.36'
  annotationProcessor 'org.projectlombok:lombok:1.18.36'
  annotationProcessor 'org.hibernate.orm:hibernate-jpamodelgen:6.6.15.Final'

  // MapStruct
  implementation 'org.mapstruct:mapstruct:1.6.3'
  annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

  testCompileOnly 'org.projectlombok:lombok:1.18.36'
  testAnnotationProcessor 'org.projectlombok:lombok:1.18.36'

  integrationTestCompileOnly 'org.projectlombok:lombok:1.18.34'
  integrationTestAnnotationProcessor 'org.projectlombok:lombok:1.18.34'

  implementation 'org.mapstruct:mapstruct:1.6.3'
  annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

  implementation group: 'org.apache.xmlgraphics', name: 'fop', version: '2.11'
  implementation group: 'org.apache.xmlgraphics', name: 'fop-core', version: '2.10'
  implementation group: 'org.apache.xmlgraphics', name: 'fop-events', version: '2.11'
  implementation group: 'org.apache.xmlgraphics', name: 'xmlgraphics-commons', version: '2.11'
  implementation group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.34'
  implementation group: 'net.sf.saxon', name: 'Saxon-HE', version: '12.6'

  testImplementation 'com.github.java-json-tools:json-schema-validator:2.2.14'
  testImplementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

  testImplementation 'org.springframework.boot:spring-boot-devtools'
  testImplementation 'org.springframework.boot:spring-boot-testcontainers'
  testImplementation 'org.testcontainers:junit-jupiter:1.21.0'
  testImplementation 'org.testcontainers:postgresql'
  testImplementation(platform('org.junit:junit-bom:5.12.2'))
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }
  testImplementation 'org.junit.platform:junit-platform-suite:1.12.2'
  testImplementation 'io.cucumber:cucumber-junit-platform-engine:7.22.2'

  testImplementation "net.serenity-bdd:serenity-core:${serenityVersion}"
  testImplementation "net.serenity-bdd:serenity-rest-assured:${serenityVersion}"
  testImplementation "net.serenity-bdd:serenity-report-resources:${serenityVersion}"

  testImplementation "net.serenity-bdd:serenity-cucumber:${serenityVersion}"
  testImplementation "net.serenity-bdd:serenity-ensure:${serenityVersion}"
}

application {
  mainClass.set('uk.gov.hmcts.opal.Application')
}

bootJar {
  archiveFileName = "opal-fines-service.jar"

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

// Gradle 7.x issue, workaround from: https://github.com/gradle/gradle/issues/17236#issuecomment-894768083
rootProject.tasks.named("processFunctionalTestResources") {
  duplicatesStrategy = 'include'
}

rootProject.tasks.named("processIntegrationTestResources") {
  duplicatesStrategy = 'include'
}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}

flyway {
  url = System.getenv('FLYWAY_URL')
  user = System.getenv('FLYWAY_USER')
  password = System.getenv('FLYWAY_PASSWORD')
  baselineOnMigrate = true
  baselineVersion = '000'
}

task migratePostgresDatabase(type: org.flywaydb.gradle.task.FlywayMigrateTask) {
  baselineOnMigrate = true
  if (project.hasProperty("dburl")) {
    url = "jdbc:postgresql://${dburl}"
  }
  println "Flyway jbdc property 'url' (derived from System property 'FLYWAY_URL'?) is: ${url}"
}
